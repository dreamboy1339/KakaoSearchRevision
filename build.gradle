// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: "java"

buildscript {
    ext {
        kotlin_version = "1.4.20"
        activityVersion = '1.1.0'
        fragmentVersion = '1.2.5'
        appCompatVersion = '1.2.0'
        constraintLayoutVersion = '2.0.2'
        coreTestingVersion = '2.1.0'
        coroutines = '1.3.9'
        lifecycleVersion = '2.2.0'
        materialVersion = '1.2.1'
        roomVersion = '2.2.5'
        retrofitVersion = '2.9.0'
        gsonVersion = '2.9.0'
        rxJavaVersion = '3.0.9'
        rxKotlinVersion = '3.0.0'
        rxAndroidVersion = '3.0.0'
        rxBindingVersion = '4.0.0'
        navigationVersion = '2.3.2'
        // testing
        junitVersion = '4.13.1'
        junitRunnerVersion = '1.1.0'
        testFragmentVersion = '1.2.4'
        espressoVersion = '3.1.0'
        androidxJunitVersion = '1.1.2'
        coroutinesVersion = '1.2.1'
        mockitoVersion = '2.19.0'
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.28-alpha'
        def nav_version = "2.3.2"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {

    repositories {
        jcenter()
    }

    configurations {
        ktlint
    }

    afterEvaluate { project ->
        check.dependsOn ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:0.40.0"
        // additional 3rd party ruleset(s) can be specified here
        // just add them to the classpath (e.g. ktlint 'groupId:artifactId:version') and
        // ktlint will pick them up
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "src/**/*.kt"
        // to generate report in checkstyle format prepend following args:
        // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
        // see https://github.com/pinterest/ktlint#usage for more
    }

    /** 스타일 수정 후 자동으로 수정해 줌 */
    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", "src/**/*.kt"
    }
}


//task clean(type: Delete) {
//    delete rootProject.buildDir
//}